apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

version = VERSION_NAME
group = GROUP

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    configuration {
        includeNonPublic = false
        reportUndocumented = true
        skipEmptyPackages = true

        perPackageOption {
            prefix = "com.github.remoteconfigdebugger.ui"
            suppress = true
        }
        perPackageOption {
            prefix = "com.github.remoteconfigdebugger.utils"
            suppress = true
        }
        perPackageOption {
            prefix = "com.github.remoteconfigdebugger.model"
            suppress = true
        }
    }
}

def dokka = tasks['dokka']

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set('javadoc')
    from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact javadocJar
                artifact sourcesJar
                groupId group
                artifactId 'library-no-op' // replace with your library artifact id
                version version
                pom {
                    name = POM_REPO_NAME
                    description = POM_DESCRIPTION
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENSE_NAME
                            url = POM_LICENSE_URL
                        }
                    }
                    scm {
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_CONNECTION
                        url = POM_URL

                    }
                    developers {
                        developer {
                            id = 'saransh9'
                            name = 'saransh gupta'
                            email = 'saransh094@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('user')
    key = properties.getProperty('apiKey')
    publications = ['release']
    configurations = ['archives']
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'RemoteConfigDebugger'
        name = 'library-no-op'
        desc = POM_DESCRIPTION
        vcsUrl = 'https://github.com/saransh9/RemoteConfigDebugger.git'
        websiteUrl = POM_URL
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}
